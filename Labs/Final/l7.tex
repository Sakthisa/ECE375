\documentclass[10pt,letterpaper]{article}
\usepackage{anysize}
\marginsize{2cm}{2cm}{1cm}{1cm}

\begin{document}

\begin{titlepage}
    \vspace*{4cm}
    \begin{flushright}
    {\huge
        ECE 375 Lab 7\\[1cm]
    }
    {\large
        Final Lab - Dont Go Ova
    }
    \end{flushright}
    \begin{flushleft}
    Lab Time: Wednesday 5-7
    \end{flushleft}
    \begin{flushright}
    Riley Hickman
    Jacques Uber
    \vfill
    \rule{5in}{.5mm}\\
    TA Signature
    \end{flushright}

\end{titlepage}



\section{Overview}
For this lab we decided to implement our own game. We call it "Don't Go Ova". The object of the game is to get as close to an unknown number by adding a random number to your hand. If you bust you loose, otherwise you win. It is a very happy game!

\section{What We Did}
We decided to use a player master relationship. The master sends commands to the player to inform them if they won or lost, or to start a new round. The master would also confirm that all the players have stayed or busted before informing them of a win/lose/new round. The player is responsible for getting a random number to add to its hand, keeping track of round and score, and telling the master its score or if it busted.

We made use of the interrupt vectors, IR communication (USART), LCD Screen, stack pointer, and data memory.

On both the master and the player boards we use state variables to determine course of actions. For the master there were two states: botid received and not received. The player also had two states: play options and hold hand (which would occur on a bust or voluntary stay).

\subsection{In the Master:}

We used an infinite loop to check if the player was active (they hadn't busted or stayed). Once the player was inactive, they were notified if they had won or lost. A NewRound signal was sent after every round.

When a transmit was received the state would change and the received word is stored as the botid, then the Master would listen for a hand value. Once the hand value is received it would be compare to the stored best\_value. If the received hand was better than the best_score (stored in a register) it would replace the best\_score and then store the botid in best\_botid. Finally, the master would decrement the number of active players.

Only one player is ever active. Once an id and hand is received the player would be informed of winning or informed that it lost. Then a new round would begin.

When a new round is called, number of active players is reset to the number of players (in our case 1), and best\_id and best\_score are set to 0.


\subsection{In the Player:}

We used polling to check for input on PORTD. The hit button is checked first then the stay button is checked. When the Hit button is pressed, they player does the "DoHit" routine. Once the player holds or busts, USART_Receive waits for the results of the round and the NewRound signal.

Players have two options hit or stay. When they hit a random number is generated and added to their hand score.
Their hand is then compared to 70 to identify if they busted. If they do not bust they have are presented with the two options, hit or stay. When stay is called, the hand score is locked and transmitted to the master for processing. The state of the player is also changed to waiting for results. A message is printed to the LCD screen saying "WAITING 4 RESULTS" until the winner/NewRound is received.

Players will bust when they hit and their score goes over 70. The player then submits the score of 0 (an automatic loss). This is dealt with in the hit function. Also, when a bust occurs the LCD prints "U WENT OVA" until a NewRound signal is received. The NewRound signal informs the player of a new round and if bot is not told of winning all ready they lost.

When a new round is received in a inactive state the round is incremented, if the wining code is received by the player,(this was their botid+1) their score is incremented. The player's hand is then set to zero and state is set to active. All these changes are shown on the LCD screen.

\section{Difficulties}

We attempted a true random, by grabbing garbage out of the USART, but it wouldn't work. Our next attempt was to get a seed value from a timer/counter again this was fruitless. We finally ended with a static seed that generates a pseudo random number based off the last value in seed.

This was intended to have multiple players, but it just wouldn't work. It was very inconsistent in its behavior with more then one player. After a long time of trying we decided to scrap that feature. It was a last resort to scrap it.

A lot of time was spent planning how to implement the game, so much of it went better that it would have been withe planning.
It was a very bumpy road yet, even with our well thought out plan for how to do it.
Though there were many small bugs that appeared, through the process which added up to a big problem.
We also had a problem that we didn't anticipate of running out of registers to use.
To fix that we got clever with the use of the stack pointer, to temporarily relieve registers for use.

We had a lot of issues with the IR transmit sending too fast, which was solved by adding delays between the sent commands. This allowed the other bot to catch up from the first of the back to back commands.

\section{Conclusion}

We would like to of had more time to make a cooler game, that had more incentives to play. Such as the bust value randomly changing, and their score based on how close they get to the bust value as well as more then one player.

This is definitely an interesting field, but it is not our preferred flavor of programming. This being one of the first 'real world' assembly projects, we found it very frustrating, but also rewarding when it would work.

\section{Source Code}
\texttt{•}
\footnotesize{•}
Master Code
\begin{verbatim}

; Riley Hickman & Jacques Uber
; ECE375 Final Lab
; master.asm

.include "m128def.inc"			; Include the ATMega128 Definition Doc
.def	mpr = r16				; Multi-purpose register defined for LCDDV2
.def	game_state = r5			; Game State
.def    best_botId = r7
.def    best_score = r8
.def    tmp_botid        = r9
.def    tmp2        = r10
                                ; State 0 = Waiting For players to send scores
.def    players_active = r11         ; Used to tell if all players have reported in their hands.
.def	ReadCnt = r23			; Counter used to read data from Program Memory
.equ	CountAddr = $0130		; Address of ASCII counter text
.def	counter = r4			; Counter used for Bin2ASCII
.def	rec = r24			; Counter used for Bin2ASCII
.equ	Round = $0200		    ; Address Of Round Count
.equ	BestBot  = $0202		    ; Address of BotId with the best score
.equ	BestScore  = $0206		    ; Address of Best Score

; Controls
.equ	BStartGame     = 0b11111110				; Right Whisker Input Bit
.equ	BStay          = 0b11111101
.equ    NewGame        = 0b10000111
.equ    NewRound       = 0b10011111

;BotId
.equ    BotID = 0b10101010
.equ    WinID = 0b10101011



.cseg							; Beginning of code segment

;***********************************************************
;*	Interrupt Vectors
;***********************************************************
.org	$0000					; Beginning of IVs
		rjmp INIT				; Reset interrupt

.org    $003C
rcall USART_Receive
reti

;***********************************************************
;*	Program Initialization
;***********************************************************
.org	$0046					; Origin of the Program, after IVs
INIT:							; Initialize Stack Pointer
		ldi		mpr, HIGH(RAMEND)
		out		SPH, mpr
		ldi		mpr, LOW(RAMEND)
		out		SPL, mpr

		rcall	LCDInit			; INITIALIZE THE LCD DISPLAY

        ; Initialize Port D for inputs
        ldi     mpr, $FF        ; Initialize Port D for inputs
        out     PORTD, mpr      ; with Tri-State
        ldi     mpr, $00        ; Set Port D Directional Register
        out     DDRD, mpr       ; for inputs

USART_INIT:
        ;Set double data rate
        ldi r16, (1<<U2X1)
        ; UCSR1A control register -- Bit 1 – U2Xn: Double the USART Transmission Speed
        sts UCSR1A, r16

        ; Initialize Port B for output
        ldi     mpr, $00        ; Initialize Port B for outputs
        out     PORTB, mpr      ; Port B outputs low
        ldi     mpr, $ff        ; Set Port B Directional Register
        out     DDRB, mpr       ; for output

        ;Set baudrate at 2400bps
        ; UBRR1H Bod rate control register
        ldi r16, high(832)
        sts UBRR1H, r16
        ldi r16, low(832)
        sts UBRR1L, r16

		;Set frame format: 8data bits, 2 stop bit
        ldi r16, (0<<UMSEL1|1<<USBS1|1<<UCSZ11|1<<UCSZ10)
        sts UCSR1C, r16

        ;Enable both receiver and transmitter -- needed for Lab 2
        ldi r16, (1<<RXCIE1|1<<RXEN1|1<<TXEN1) ; RXEN (Receiver enable) TXEN (Transmit enable)
        sts UCSR1B, r16

		; Activate interrupts
		sei						; Turn on interrupts
        ldi     mpr, 1
        mov     players_active, mpr
        clr     game_state
MAIN:
        ; Loop until all players have checked in.
        mov     mpr, players_active
        cpi     mpr, 0
        breq    START_NEWROUND
        rjmp MAIN

START_NEWROUND:
        ; All players have checked in.
        ; Start new round

        ldi     rec, 100
        call    Do_Wait
        mov     mpr, best_score
        cpi     mpr, 0
        ; Make sure everyone didn't bust.
        breq    EVERYONE_BUSTS
        rjmp    SEND_WINNER
EVERYONE_BUSTS:
        clr     best_botId ; If everyone busted, set best_botId to zero
SEND_WINNER:

        mov     mpr, best_botId
        inc     mpr
        call    USART_Transmit ; Send winning botId
        ldi     rec, 200
        call    Do_Wait

        ldi     mpr, 1
        mov     players_active, mpr

        clr     best_score
        clr     game_state
        ldi     mpr, NewRound
        call    USART_Transmit ; Send new round command
rjmp MAIN

.include "LCDDriver.asm"		; Include the LCD Driver
;----------------------------------------------------------------
; Sub: SetBestScore
; Desc: Store mpr into address at BestScore
;----------------------------------------------------------------
SetBestScore:
        push XL
        push XH
		ldi		XL, low(BestScore) ; Get Score
		ldi		XH, high(BestScore)
        st      X, mpr
        pop XH
        pop XL
        ret

;----------------------------------------------------------------
; Sub: USART_Receive
; Desc: Receive data over IR Calculate Bot with best score
;   Make sure it's not one of our commands
;   if game_state == 0:
;       receive BotId
;       tmp_botid = BotId
;   if game_state == 1:
;       if rec > BestHand:
;           mov  tmp_botid, best_botId
;           mov  rec, best_score
;           clr  tmp_botid
;           ldi  game_state, 0
;----------------------------------------------------------------
USART_Receive:
    ; Wait for data to be received
    lds mpr, UCSR1A
    sbrs mpr, RXC1
    rjmp USART_Receive

    ; Get and return receive data from receive buffer
    lds rec, UDR1 ; rec has what was received

    cpi     rec, NewRound
    breq    DONE_Rec
    mov     r23, best_botId
    inc     r23
    cp      rec, r23
    breq    DONE_Rec

    mov     mpr, game_state ; rec is tmp_botid
    cpi     mpr, 0
    breq    REC_BOTID  ; jump to State 0
    rjmp    REC_SCORE  ; Goto to State 1
REC_BOTID: ; State is 0
    ldi     mpr, 16
    out     PORTB, mpr
    mov     tmp_botid, rec ; tmp_botid not has BotId
    inc     game_state ; set state to 1
    rjmp    DONE_Rec

REC_SCORE: ; State is 1
    ldi     mpr, 1
    out     PORTB, mpr
    mov     mpr, best_score
    sub     mpr, rec        ; 0 =< |rec (recieved) - mpr (best_score) | =< 71
            ; If mpr > rec Then After sub mpr > 0   recieved is the better score
            ; If mpr < rec Then After sub mpr < 0   best_score is still best score
    cpi     mpr, 0
    brge    STILL_BEST      ; TODO, is this right?

    ; Update best score
    mov     best_botId, tmp_botid
    mov     best_score, rec
    clr     tmp_botid
    clr     game_state
    dec     players_active
    rjmp    DONE_Rec

STILL_BEST:
    clr     game_state
    dec     players_active

DONE_Rec:
    ret
;----------------------------------------------------------------
; Sub: USART_Transmit
; Desc: Send data over IR
;       Sends value found in mpr and then returns
;----------------------------------------------------------------
USART_Transmit:
    lds rec, UCSR1A
    sbrs rec, UDRE1
    ; Load status of USART1
    ; Loop until transmit data buffer is ready
    rjmp USART_Transmit
    ; Send data
    sts UDR1, mpr
    ret

;----------------------------------------------------------------
; Sub:	Wait
; Desc:	A wait loop that is 16 + 159975*waitcount cycles or roughly
;		waitcount*10ms.  Just initialize wait for the specific amount
;		of time in 10ms intervals. Here is the general eqaution
;		for the number of clock cycles in the wait loop:
;			((3 * line + 3) * type + 3) * waitcount + 13 + call
;----------------------------------------------------------------
Do_Wait:
		push	rec			; Save wait register
		push	line			; Save line register
		push	type			; Save type register

Loop:	ldi		type, 224		; load type register
OLoop:	ldi		line, 237		; load line register
ILoop:	dec		line			; decrement line
		brne	ILoop			; Continue Inner Loop
		dec		type		    ; decrement type
		brne	OLoop			; Continue Outer Loop
		dec		rec		; Decrement wait
		brne	Loop			; Continue Wait loop

		pop		type		    ; Restore type register
		pop		line		    ; Restore line register
		pop		rec		; Restore wait register
		ret				; Return from subroutine
\end{verbatim}

Player Code
\begin{verbatim}
; Riley Hickman & Jacques Uber
; ECE375 Final Lab
; player.asm

.include "m128def.inc"			; Include the ATMega128 Definition Doc
.def	mpr = r16				; Multi-purpose register defined for LCDDV2
.def	game_state = r5			; Game State
                                ; State 0 = Player can hit or stay.
                                ; State 1 = Player Has hit waiting for results.
.def    rec = r6
.def    tmp = r7
.def	ReadCnt = r23			; Counter used to read data from Program Memory
.equ	CountAddr = $0130		; Address of ASCII counter text
.def	counter = r4			; Counter used for Bin2ASCII
.def	waitcount = r24			; Counter used for Bin2ASCII
.equ	Round = $0200		    ; Address Of Round Count
.equ	Score = $0202		    ; Address of Score Count
.equ	Hand  = $0206		    ; Address of Hand Count


.equ    BustVal = 71
; Controls
.equ	BHit     = 0b11111110				; Right Whisker Input Bit
.equ	BStay    = 0b11111101

.equ    BustCode = 0b00000000

.equ    NewGame  = 0b10000111
.equ    NewRound = 0b10011111

;BotId
.equ    BotID = 0b11101010
.equ    WinID = 0b11101011

; Random Number Stuff
.equ    Seed  = $0208               ; Address of Random Seed
.equ    rndmul = 0b01010111
.equ    rndinc = 0b11010101



.cseg							; Beginning of code segment

;***********************************************************
;*	Interrupt Vectors
;***********************************************************
.org	$0000					; Beginning of IVs
		rjmp INIT				; Reset interrupt

.org    $003C
rcall USART_Receive
reti

;***********************************************************
;*	Program Initialization
;***********************************************************
.org	$0046					; Origin of the Program, after IVs
INIT:							; Initialize Stack Pointer
		ldi		mpr, HIGH(RAMEND)
		out		SPH, mpr
		ldi		mpr, LOW(RAMEND)
		out		SPL, mpr

		rcall	LCDInit			; INITIALIZE THE LCD DISPLAY

        ; Initialize Port D for inputs
        ldi     mpr, $FF        ; Initialize Port D for inputs
        out     PORTD, mpr      ; with Tri-State
        ldi     mpr, $00        ; Set Port D Directional Register
        out     DDRD, mpr       ; for inputs

USART_INIT:
        ;Set double data rate
        ldi r16, (1<<U2X1)
        ; UCSR1A control register -- Bit 1 – U2Xn: Double the USART Transmission Speed
        sts UCSR1A, r16

        ;Set baudrate at 2400bps
        ; UBRR1H Bod rate control register
        ldi r16, high(832)
        sts UBRR1H, r16
        ldi r16, low(832)
        sts UBRR1L, r16

		;Set frame format: 8data bits, 2 stop bit
        ldi r16, (0<<UMSEL1|1<<USBS1|1<<UCSZ11|1<<UCSZ10)
        sts UCSR1C, r16

        ;Enable both receiver and transmitter -- needed for Lab 2
        ldi r16, (1<<RXCIE1|1<<RXEN1|1<<TXEN1) ; RXEN (Receiver enable) TXEN (Transmit enable)
        sts UCSR1B, r16

		; Activate interrupts
		sei						; Turn on interrupts

        ldi		XL, low(Round) ; Get Round
        ldi		XH, high(Round)
        ldi     mpr, 0 ; Initialize round
        st      X, mpr

        ldi		XL, low(Score) ; Get Score
        ldi		XH, high(Score)
        ldi     mpr, 0 ; Initialize score
        st      X, mpr
        ; Generate random seed.
        ldi     mpr, 9
        sts     Seed, mpr
        lds     R0, Seed
        clr     mpr

        ; Set state to 0
        ldi     mpr, 0
        mov     game_state, mpr

        clr     mpr
        call    SetHand

MAIN:
PLAY_GAME:
        call    Playing_LN1   ; Print score and round to the first line of the LED
        ldi     waitcount, 10
        call    Do_Wait

        in      mpr, PIND         ; Get PIND
        cpi     mpr, BHit
        breq    PLAYER_HIT        ; Did the player hit?
        rjmp    CHECK_STAY
PLAYER_HIT:
        call    DoHit             ; The player hit
        ldi     waitcount, 20 ; Give some delay so we don't over do it on the buttons
        call    Do_Wait

CHECK_STAY:
        in      mpr, PIND       ; Get whisker input from Port D
        cpi     mpr, BStay      ; The player stayed
        breq    PLAYER_STAY     ; Right
        rjmp    PLAYER_DONE
PLAYER_STAY:
        call    DoStay
PLAYER_DONE:

rjmp PLAY_GAME


.include "LCDDriver.asm"		; Include the LCD Driver

;***********************************************************
;*	 LCD Code
;***********************************************************
;----------------------------------------------------------------
; Sub: NewRound:
; Desc: Print winning message
;----------------------------------------------------------------
PrintNewRound:
        push mpr
        push line
        push count
		ldi		ZL, low(NEW_ROUND<<1); Init variable registers
		ldi		ZH, high(NEW_ROUND<<1)
		ldi		YL, low(LCDLn2Addr)
		ldi		YH, high(LCDLn2Addr)
		ldi		ReadCnt, LCDMaxCnt
        rjmp    INIT_LINE2meta
;----------------------------------------------------------------
; Sub: LooseRound:
; Desc: Print loosing message
;----------------------------------------------------------------
PrintLooseRound:
        push mpr
        push line
        push count
		ldi		ZL, low(LOSE<<1); Init variable registers
		ldi		ZH, high(LOSE<<1)
		ldi		YL, low(LCDLn2Addr)
		ldi		YH, high(LCDLn2Addr)
		ldi		ReadCnt, LCDMaxCnt
        rjmp    INIT_LINE2meta
;----------------------------------------------------------------
; Sub: WinRound:
; Desc: Print winning message
;----------------------------------------------------------------
PrintWinRound:
        push mpr
        push line
        push count
		ldi		ZL, low(WIN<<1); Init variable registers
		ldi		ZH, high(WIN<<1)
		ldi		YL, low(LCDLn2Addr)
		ldi		YH, high(LCDLn2Addr)
		ldi		ReadCnt, LCDMaxCnt
        rjmp    INIT_LINE2meta
;----------------------------------------------------------------
; Sub: Stay:
; Desc: Print winning message
;----------------------------------------------------------------
PrintStay:
        push mpr
        push line
        push count
		ldi		ZL, low(SAY_STAY<<1); Init variable registers
		ldi		ZH, high(SAY_STAY<<1)
		ldi		YL, low(LCDLn2Addr)
		ldi		YH, high(LCDLn2Addr)
		ldi		ReadCnt, LCDMaxCnt
        rjmp    INIT_LINE2meta

;----------------------------------------------------------------
; Sub: PrintBust
; Desc: Print winning message
;----------------------------------------------------------------
PrintBust:
        push mpr
        push line
        push count
		ldi		ZL, low(BUST_LINE<<1); Init variable registers
		ldi		ZH, high(BUST_LINE<<1)
		ldi		YL, low(LCDLn2Addr)
		ldi		YH, high(LCDLn2Addr)
		ldi		ReadCnt, LCDMaxCnt
        rjmp    INIT_LINE2meta

; This does the actual printing.
INIT_LINE2meta:
        lpm		mpr, Z+			; Read Program memory
        st		Y+, mpr			; Store into memory
        dec		ReadCnt			; Decrement Read Counter
        brne	INIT_LINE2meta	; Continue untill all data is read
        rcall	LCDWrLn2		; WRITE LINE 1 DATA
        pop count
        pop line
        pop mpr
        ret

;----------------------------------------------------------------
; Sub: Playing_LN2:
; +-------------------------------------------+
; | Hit/Stay                        HAND: XX  |
; +-------------------------------------------+
; ^ Write that to line 2 of LCD.
; X -- Hand count stored in `Hand`
;
;----------------------------------------------------------------
playing_ln2:
        push mpr
        push line
        push count

		ldi		ZL, low(SHOW_HAND<<1); Init variable registers
		ldi		ZH, high(SHOW_HAND<<1)
		ldi		YL, low(LCDLn2Addr)
		ldi		YH, high(LCDLn2Addr)
		ldi		ReadCnt, LCDMaxCnt
INIT_LINE2hand:
        lpm		mpr, Z+			; Read Program memory
        st		Y+, mpr			; Store into memory
        dec		ReadCnt			; Decrement Read Counter
        brne	INIT_LINE2hand	; Continue untill all data is read
        rcall	LCDWrLn2		; WRITE LINE 1 DATA

        ; Write current hand
        ; Write Round
        call Clear_data_area
		ldi		XL, low(Hand) ; Get Round
		ldi		XH, high(Hand)
        ld      mpr, X
        ; Convert mpr to ASCII
		ldi		XL, low(CountAddr)
		ldi		XH, high(CountAddr)
		rcall	Bin2ASCII		; CALL BIN2ASCII TO CONVERT DATA
								; NOTE, COUNT REG HOLDS HOW MANY CHARS WRITTEN
		ldi		ReadCnt, 2		; always write three chars to overide existing data in LCD
        ldi     count, 14
        ldi     line, 2
        ; Write hand
T0_L3:	ld		mpr, X+			; LOAD MPR WITH DATA TO WRITE
		rcall	LCDWriteByte	; CALL LCDWRITEBYTE TO WRITE DATA TO LCD DISPLAY
		inc		count			; INCREMENT COUNT TO WRITE TO NEXT LCD INDEX
		dec		ReadCnt			; decrement read counter
		brne	T0_L3			; Countinue untill all data is written

        pop count
        pop line
        pop mpr
        ret

;----------------------------------------------------------------
; Sub: Playing_LN1:
; +-------------------------------------------+
; | Round  X                         (Score)  |
; +-------------------------------------------+
; ^ Write that to line 1 of LCD.
; X -- Round number stored in `round` register
; Score -- Players Score stored in `score` register
;
; Write initial "Round:% Score:%" string to LCD line 1
;----------------------------------------------------------------
Playing_LN1:
        push mpr
        push line
        push count

		ldi		ZL, low(TXT0<<1); Init variable registers
		ldi		ZH, high(TXT0<<1)
		ldi		YL, low(LCDLn1Addr)
		ldi		YH, high(LCDLn1Addr)
		ldi		ReadCnt, LCDMaxCnt
INIT_LINE1:
        lpm		mpr, Z+			; Read Program memory
        st		Y+, mpr			; Store into memory
        dec		ReadCnt			; Decrement Read Counter
        brne	INIT_LINE1		; Continue untill all data is read
        rcall	LCDWrLn1		; WRITE LINE 1 DATA
        ; Write Round and Score
WRITE_ROUND:
        ; Write Round
        call Clear_data_area
		ldi		XL, low(Round) ; Get Round
		ldi		XH, high(Round)
        ld      mpr, X
        ; Convert mpr to ASCII
		ldi		XL, low(CountAddr)
		ldi		XH, high(CountAddr)
		rcall	Bin2ASCII		; CALL BIN2ASCII TO CONVERT DATA
								; NOTE, COUNT REG HOLDS HOW MANY CHARS WRITTEN
		ldi		ReadCnt, 2		; always write three chars to overide existing data in LCD
        ldi     count, 6
        ldi     line, 1
        ; Write Score to LCD
T0_L1:	ld		mpr, X+			; LOAD MPR WITH DATA TO WRITE
		rcall	LCDWriteByte	; CALL LCDWRITEBYTE TO WRITE DATA TO LCD DISPLAY
		inc		count			; INCREMENT COUNT TO WRITE TO NEXT LCD INDEX
		dec		ReadCnt			; decrement read counter
		brne	T0_L1			; Countinue untill all data is written

WRITE_SCORE:
        ; Write Score
        call Clear_data_area
		ldi		XL, low(Score) ; Get Score
		ldi		XH, high(Score)
        ld      mpr, X
        ; Convert mpr to ASCII
		ldi		XL, low(CountAddr)
		ldi		XH, high(CountAddr)
		rcall	Bin2ASCII		; CALL BIN2ASCII TO CONVERT DATA
								; NOTE, COUNT REG HOLDS HOW MANY CHARS WRITTEN
		ldi		ReadCnt, 2		; always write three chars to overide existing data in LCD
        ldi     count, 14
        ldi     line, 1

        ; Write Score to LCD
T0_L2:	ld		mpr, X+			; LOAD MPR WITH DATA TO WRITE
		rcall	LCDWriteByte	; CALL LCDWRITEBYTE TO WRITE DATA TO LCD DISPLAY
		inc		count			; INCREMENT COUNT TO WRITE TO NEXT LCD INDEX
		dec		ReadCnt			; decrement read counter
		brne	T0_L2			; Countinue untill all data is written

        pop count
        pop line
        pop mpr
        ret

;----------------------------------------------------------------
; Sub: Clear_data_area
; Desc: ZERO out 2 words starting at CountAddr
;----------------------------------------------------------------

Clear_data_area:
		ldi		XL, low(CountAddr)
		ldi		XH, high(CountAddr)
		ldi		count, 2		; Init X-ptr and count
		ldi		mpr, ' '		; Load mpr with space char
LABEL1:	st		X+, mpr			; Clear data area
		dec		count			; Decrement count
		brne	LABEL1			; Continue until all data is cleared
        ret

;***********************************************************
;*	Functions and Subroutines
;***********************************************************
;----------------------------------------------------------------
; Sub: DoHit
; Desc: The player wants to hit.
;----------------------------------------------------------------
DoHit:
    clr     mpr
    call    GetHand
    mov     tmp, mpr ;tmp = hand
    call    GetRand  ; mpr = Random number
    add     mpr, tmp ; mpr = Hand + Random number
    call    SetHand  ; Set hand Value
    subi    mpr, BustVal ; Hand = Hand - 70
    cpi     mpr, 0
    brge    BUST    ; mpr > 0 ? Busted: Return
    ; They didn't go over
    call    Playing_LN2
    ret
BUST:
    ; They busted
    ldi     mpr, BotId
    call    USART_Transmit  ; Send BotId

    ldi     waitcount, 10
    call    Do_Wait

    ldi     mpr, BustCode
    call    USART_Transmit  ; Send Bust code
    call    PrintStay
    ldi     waitcount, 100
    ldi     mpr, 1
    mov     game_state, mpr ; Go to state 1, waiting for reply

    call    PrintBust
    ldi     waitcount, 100
    call    Do_Wait

    rjmp STATE1


;----------------------------------------------------------------
; Sub: DoStay
; Desc: The player wants to hit.
;----------------------------------------------------------------
DoStay:
    call    PrintStay


    ldi     mpr, 0
    mov     game_state, mpr ; Go to state 0, waiting for reply
    ldi     mpr, BotId
    call    USART_Transmit  ; Send BotId
    ldi     waitcount, 10
    call    Do_Wait
    call    GetHand         ; Stores hand into mpr
    call    USART_Transmit  ; Send Hand

    ; Let the light fade
    ldi     waitcount, 10
    call    Do_Wait
    ldi     mpr, 1
    mov     game_state, mpr ; Go to state 1, waiting for reply
STATE1:
    mov     mpr, game_state
    cpi     mpr, 1
    breq    STATE1          ; If we are in state 2 Go play game
    ldi     mpr, 0
    mov     game_state, mpr

    ret

;----------------------------------------------------------------
; Sub: IncScore
; Desc: Incriment the score
;----------------------------------------------------------------
IncScore:
        push XL
        push XH
        push mpr
		ldi		XL, low(Score) ; Get Score
		ldi		XH, high(Score)
        ld      mpr, X
        inc     mpr
        st      X, mpr
        pop mpr
        pop XH
        pop XL
        ret
;----------------------------------------------------------------
; Sub: IncScore
; Desc: Incriment the score
;----------------------------------------------------------------
IncRound:
        push XL
        push XH
        push mpr
		ldi		XL, low(Round) ; Get Round
		ldi		XH, high(Round)
        ld      mpr, X
        inc     mpr
        st      X, mpr
        pop mpr
        pop XH
        pop XL
        ret

;----------------------------------------------------------------
; Sub: SetHand
; Desc: Set memory at hand to value of mpr
;----------------------------------------------------------------
SetHand:
        push XL
        push XH
		ldi		XL, low(Hand) ; Get Round
		ldi		XH, high(Hand)
        st      X, mpr
        pop XH
        pop XL
        ret
;----------------------------------------------------------------
; Sub: GetHand
; Desc: Set memory at hand to value of mpr
; Stores into mpr, clobers mpr
;----------------------------------------------------------------
GetHand:
        push XL
        push XH
		ldi		XL, low(Hand) ; Get Round
		ldi		XH, high(Hand)
        ld      mpr, X
        pop XH
        pop XL
        ret
;----------------------------------------------------------------
; Sub: USART_Receive
; Desc: Receive data over IR
;       This is where checking to see if you won happens.
;       if state != 1:
;           ret
;       if rec == NewGame
;           call IncRound
;           // Other stuff?
;       if rec == NewRound
;           call PrintLoose
;           call IncRound
;           Reset hand
;           state = 2
;       if rec == WinID
;           call PrintWin
;           call IncRound
;           call IncScore
;           Reset hand
;           state = 2
;----------------------------------------------------------------
USART_Receive:
    ; Wait for data to be received
    lds mpr, UCSR1A
    sbrs mpr, RXC1
    rjmp USART_Receive

    ; Get and return receive data from receive buffer
    lds mpr, UDR1
    ; mpr === rec

    ldi     waitcount, 1
    cp      waitcount, game_state
    brne    DONE_REC


    cpi     mpr, NewGame     ;   if rec == NewGame
    breq    NEW_GAME

    cpi     mpr, WinId       ;   if rec == WinId
    breq    WIN_ROUND

    cpi     mpr, NewRound    ;   if rec == NewRound
    breq    NEXT_ROUND

    rjmp DONE_REC            ; ???

NEXT_ROUND:
    call    PrintLooseRound  ; We lost
    call    IncRound         ; Round = Round + 1
    clr     mpr
    call    SetHand          ; Reset Hand


    clr     mpr
    call    SetHand          ; Reset Hand

    clr     game_state      ; Set game state to 0

    call    PrintNewRound
    ldi     waitcount, 100
    call    Do_Wait
    call    Playing_LN2
    rjmp    DONE_REC
WIN_ROUND:
    call    PrintWinRound    ; We Won
    call    IncRound         ; Round = Round + 1
    call    IncScore

    clr     mpr
    call    SetHand          ; Reset Hand

    clr     game_state      ; Set game state to 0

    ldi     waitcount, 100
    call    Do_Wait
    call    Playing_LN2
    rjmp    DONE_REC

NEW_GAME:
    rjmp    DONE_REC

DONE_REC:
    ret

;----------------------------------------------------------------
; Sub: USART_Transmit
; Desc: Send data over IR
;       Sends value found in mpr and then returns
;----------------------------------------------------------------

USART_Transmit:
    push waitcount ; Use waitcount as garbage variable.
    lds waitcount, UCSR1A
    sbrs waitcount, UDRE1
    ; Load status of USART1
    ; Loop until transmit data buffer is ready
    rjmp USART_Transmit
    ; Send data
    sts UDR1, mpr
    pop waitcount
    ret
;----------------------------------------------------------------
; Sub:	GetRand
; Desc:	Generage a random number and store it in mpr
;----------------------------------------------------------------
GetRand:
        ldi     mpr, rndmul
        lds     R0, Seed
        mul     R0, mpr
        ldi     mpr, rndinc
        add     R0, mpr
        sts     Seed, R0
        ldi     mpr, 0b00011101
        and     R0, mpr
        inc     R0      ;Should now have a random number from 1-30 in R0
        mov     mpr, R0
        ret
;----------------------------------------------------------------
; Sub:	Wait
; Desc:	A wait loop that is 16 + 159975*waitcount cycles or roughly
;		waitcount*10ms.  Just initialize wait for the specific amount
;		of time in 10ms intervals. Here is the general eqaution
;		for the number of clock cycles in the wait loop:
;			((3 * line + 3) * type + 3) * waitcount + 13 + call
;----------------------------------------------------------------
Do_Wait:
		push	waitcount		; Save wait register
		push	line			; Save line register
		push	type			; Save type register

Loop:	ldi		type, 224		; load type register
OLoop:	ldi		line, 237		; load line register
ILoop:	dec		line			; decrement line
		brne	ILoop			; Continue Inner Loop
		dec		type		    ; decrement type
		brne	OLoop			; Continue Outer Loop
		dec		waitcount		; Decrement wait
		brne	Loop			; Continue Wait loop

		pop		type		    ; Restore type register
		pop		line		    ; Restore line register
		pop		waitcount		; Restore wait register
		ret				; Return from subroutine
TXT0:
.DB "Round:% Score:% " ; Values are at 6 and 14 (starting at zero offset)
WIN:
.DB "  U WIN ROUND!  " ;
LOSE:
.DB " U NO WIN ROUND " ;
BUST_LINE:
.DB "   U WENT OVA   " ;
NEW_ROUND:
.DB "   NEW ROUND!   " ;
SAY_STAY:
.DB " WAIT 4 RESULTS " ;
SHOW_HAND:
.DB "Hit/Stay HAND:%%" ; Values at 14 and 1500
\end{verbatim}
\end{document}
